// import Drew's lattice code
Attach("/projects/41d9a393-6a13-4259-bc6e-fa8eb804117b/threetorsimage/GSp.m");

L := GSpLattice(4, 5, 0);

// This function computes whether a subgroup contains an involution with similitude -1
function possible_image(label)
    M := Matrix(IntegerRing(5), [[0, 0, 0, 1], 
                [0, 0, 1, 0], 
                [0, 4, 0, 0],
                [4, 0, 0,0]]); // this is the explicit symplectic form
    for elt in L[label]`subgroup do
        elt_ord := Order(elt);
        sim_character := (Transpose(elt)*M*elt)[1,4];
        // per chidambaram we need to check for existence of an order two element with similitude character -1
        if (elt_ord eq 2) and (sim_character eq 4) then
            return true;
        end if;
    end for;
    return false;
end function;

// exclude GSp4 itself
Remove(~L, "1.1.1"); 

// exclude impossible subgroups
for subgroup in L do
    if not(possible_image(subgroup`label)) then
        Remove(~L, subgroup`label);
    end if;
end for;

// Given a finite matrix group, computes the characteristic polynomial and 
// eigenspace dimensions of the elements (only for eigenvalues in ground field)
CharDimSpectrum := function(MG) // MG is a finite matrix group
    return {* <CharacteristicPolynomial(M), Dimension(Eigenspace(M, 1))>: M in MG *};
end function;

Likelihood := function(sampled, expected)
    E := 1/#expected;
    prob := 0;
    is_possible := true;

    for elt in Set(sampled) do
        if Multiplicity(expected, elt) eq 0 then
            is_possible := false;
            continue;
        end if;
        prob := prob + Log(Multiplicity(expected, elt)*E)*Multiplicity(sampled, elt);
    end for;
    return is_possible, prob;
end function;

//////////////////////////// ^^ SETUP ^^ ///////////////////////////////
////////////////////////// vv bucketer vv //////////////////////////////

print "Calculating spectra";
specs := AssociativeArray();
for grp_name in Keys(L) do
    specs[grp_name] := CharDimSpectrum(L[grp_name]`subgroup);
end for;
print "Spectra calculated";

print "Bucketing";
S := [];
for grp1 in Keys(L) do
    bucket := [];
    best_likelihood := 1;
    for grp2 in Keys(L) do
        is_possible, cur_likelihood := Likelihood(specs[grp1], specs[grp2]);
			if is_possible eq false then
				continue;
			end if;
        if best_likelihood eq 1 then
    		best_likelihood := cur_likelihood;
    		bucket := [grp2];
    	elif Abs(cur_likelihood - best_likelihood) lt 0.0001 then
    		Append(~bucket, grp2);		
    	elif cur_likelihood gt best_likelihood then
    		best_likelihood := cur_likelihood;
    		bucket := [grp2];
    	end if;
    end for;
    Append(~S, Set(bucket));
end for;
print "Done bucketing";

S := Set(S);

#S
